buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://maven.minecraftforge.net/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

// change FG2's default maven url to the new one.
repositories.each {
    if (it instanceof MavenArtifactRepository && it.url.toString() == 'https://files.minecraftforge.net/maven') {
        it.url = 'https://maven.minecraftforge.net/'
    }
}

repositories {
    maven {
        name = 'blamejared'
        url = 'https://maven.blamejared.com/'
    }
    
    maven {
        name = 'jei'
        url = 'https://dvs1.progwml6.com/files/maven/'
    }

    maven {
        name = 'cursemaven'
        url = 'https://cfa2.cursemaven.com/'
    }
}

version = '1.1.2'
group = 'quaternary.youreanexpertharry'
archivesBaseName = 'youre_an_expert_harry'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'
    mappings = 'snapshot_20171003'
}

dependencies {
    runtime 'com.blamejared:MTLib:3.0.5.15'
    runtime 'CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.674'
    deobfCompile 'vazkii.botania:Botania:r1.10-363.148'

    runtime 'mezz.jei:jei_1.12.2:4.16.1.302'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
